//hidden pmtiles = "pmtiles://https://hfu.github.io/worldpop-h3/pop.pmtiles"
hidden pmtiles = "pmtiles://https://data.source.coop/smartmaps/h3ys-worldpop/tls.pmtiles"
hidden layer = "pop"
hidden key = 2020

version = 8
sources {
  a { type = "vector" url = pmtiles attribution = "WorldPop" }
}
layers = new Listing {
  new {
    id = "background" type = "background"
    paint { `background-color` = "#000" }
  }
  new {
    id = layer type = "fill" source = "a"
    `source-layer` = layer
    paint {
      `fill-opacity` = new Listing {
        "min"
        1.0
        new Listing { "/" 
          new Listing { "log10"
            new Listing {
              "+"
              1
              new Listing { "get" "\(key)" }
            }
          }
          4.0
        } 
      }
      `fill-color` = new Listing { "case"
        new Listing { "any"
          new Listing { "<" new Listing { "get" "\(key)" } 50 }
          new Listing { "<" new Listing { "get" "\(key - 1)" } 10 }
        }
        "#888888"
        new Listing {
          "interpolate-hcl"
          new Listing { "linear" }
          new Listing { "-" 
            new Listing { "ln" new Listing { "get" "\(key)" } }
            new Listing { "ln" new Listing { "get" "\(key - 1)" } }
          }
          -0.20
          "rgb(39,42,149)"
          -0.15
          "rgb(39,42,197)"
          -0.10
          "rgb(39,42,246)"
          -0.05
          "rgb(141,144,249)"
          0
          "rgb(243,246,255)"
          0.05
          "rgb(243,246,117)"
          0.10
          "rgb(230,151,92)"
          0.15
          "rgb(226,83,79)"
          0.2
          "rgb(226,83,153)"
          0.25
          "rgb(226,83,249)"
        } 
      }
    }
  }
}
